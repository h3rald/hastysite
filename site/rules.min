'hastysite import

;Routing
(
  (dict) expect -> =meta
  meta /id :id
  meta /ext :ext
  (
    ((id "home" ==) (
      meta (
        ("index" %id)
        (".html" %ext)
      ) tap
    ))
    ((true) (
      meta (
        (".html" %ext)
        ("$1/index" (id) => % %id)
      ) tap
    ))
  ) case
) :set-destination

;Process Markdown content
(
  (dict) expect -> =meta
  "" :page
  "" :contents
  meta /content-type :tpl
  meta (
    (input-fread @contents meta)
    (settings /title %site-title)
    (settings /description %site-description)
    (=temp contents temp markdown @contents temp)
    (contents %contents)
    (=temp tpl temp mustache @page temp)
    (page %contents)
  ) tap
) :process-content

;Process CSS asset
(
  (dict) expect -> =meta
  "" :contents
  meta (
    (input-fread @contents meta)
    (=temp contents preprocess-css @contents temp)
    (contents %contents)
  ) tap
  output-fwrite
) :process-css-asset

;;; Main ;;;

;Filter and sort posts by timestamp
contents (/content-type "post" ==) filter (=a =b a /timestamp  b /timestamp >) sort =posts

;Process contents
contents (
  (dict) expect -> =content
  content (/id "news" ==) (content posts %posts #content) when
  content process-content set-destination output-fwrite
) foreach

;Process assets
assets (
  (dict) expect -> 
  dup
  (
    ((/ext ".css" match) (process-css-asset))
    ((/id "^\." match) ()) ;Ignore hidden files
    ((true) (output-cp))
  ) case
) foreach
